dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)

if test "x${prefix}" != "x/usr" || test "x${sysconfdir}" != "x/etc"
then
  echo
  echo "*** The Ximian Setup Tools may not work with the default installation"
  echo "*** paths. If you're running Linux, and you installed most of the other"
  echo "*** GNOME components with a package manager, we suggest you configure"
  echo "*** this package like this:"
  echo "***"
  echo "*** ./configure --prefix=/usr --sysconfdir=/etc"
  echo "***"
  echo "*** This will be more likely to work. Thank you for your patience."
  echo
  
  sleep 5
fi

AM_INIT_AUTOMAKE(ximian-setup-tools, 0.4.2)
AM_CONFIG_HEADER(config.h)

dnl Pick up the Gnome macros.
AM_ACLOCAL_INCLUDE(macros)

GNOME_INIT
AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AM_PROG_LIBTOOL
AC_HEADER_STDC
AM_PROG_XML_I18N_TOOLS

GNOME_COMPILE_WARNINGS
GNOME_X_CHECKS
GNOME_XML_CHECK
AM_PATH_GDK_PIXBUF
AM_PATH_LIBGLADE(,,"gnome")

AC_CHECK_LIB(crack, FascistCheck, ,[
  AC_MSG_WARN(),
  AC_MSG_WARN(---------- Building without cracklib is not recommended. ----------),
  AC_MSG_WARN(),
  AC_MSG_WARN(-- If you are looking for cracklib you can try  :),
  AC_MSG_WARN(--    http://www.users.dircon.co.uk/~crypto/),
  AC_MSG_WARN(--    or :),
  AC_MSG_WARN(--    http://www.rpmfind.net/linux/rpm2html/search.php?query=cracklib),
  AC_MSG_WARN()])
if test x"$HAVE_LIBCRACK" = xyes ; then
  AC_CHECK_HEADER(crack.h, ,[
  AC_MSG_ERROR(cracklib crack.h header file not found.)])
fi

AC_CHECK_LIB(crypt, crypt, ,[
  AC_MSG_ERROR(crypt library is required.)])
AC_CHECK_HEADER(crypt.h, ,[
  AC_MSG_ERROR(crypt.h header file not found.)])

dnl ==================
dnl GdkPixbuf checking
dnl ==================
AC_MSG_CHECKING(for Gdkpixbuf libraries)
if gnome-config --libs gdk_pixbuf gnomecanvaspixbuf > /dev/null 2>&1; then 
    AC_MSG_RESULT(found)
else
    AC_MSG_ERROR(Did not find gdkpixbuf installed)
fi

dnl ==============
dnl Libglade check
dnl ==============
AC_MSG_CHECKING(for libglade libraries)
if gnome-config --libs libglade > /dev/null 2>&1; then
    AC_MSG_RESULT(found)
else
    AC_MSG_ERROR(Did not find libglade installed)
fi

dnl ======================
dnl Gnome App Lib checking
dnl ======================
AC_MSG_CHECKING(for Gnome App libraries (GAL) >= 0.5.99.12)
if gnome-config --libs gal > /dev/null 2>&1; then
	vers=`gnome-config --modversion gal | sed -e "s/gal-//" -e 's/cvs$//' -e 's/pre$//' | \
		 awk 'BEGIN { FS = "."; } { print $1 * 1000000 + $2 * 10000 + $3 * 100 + $4;}'`
	if test "$vers" -ge 59912; then
		AC_MSG_RESULT(found)
	 else
		AC_MSG_ERROR(You need at least GNOME Application libs 0.5.99.12 for this version of ximian-setup-tools)
	fi
else
	AC_MSG_ERROR(Did not find GnomeAppLib (GAL) installed)
fi

GAL_LIBS="`gnome-config --libs gal print`"
GAL_CFLAGS="`gnome-config --cflags gal`"

AC_SUBST(GAL_LIBS)
AC_SUBST(GAL_CFLAGS)


GDK_PIXBUF_LIBS="`gnome-config --libs gdk_pixbuf gnomecanvaspixbuf`"
GDK_PIXBUF_CFLAGS="`gnome-config --cflags gdk_pixbuf gnomecanvaspixbuf`"

AC_SUBST(GDK_PIXBUF_LIBS)
AC_SUBST(GDK_PIXBUF_CFLAGS)

dnl Add the languages which your application supports here.
ALL_LINGUAS="az da de es fr gl hu it ja ko no pl pt_BR ru sk sv tr uk wa"
AM_GNOME_GETTEXT

dnl Set PACKAGE_LOCALE_DIR in config.h.
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale")
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale")
fi

dnl --- Set SCRIPTS_DIR and PIXMAPS_DIR runtime and in config.h ---

if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(SCRIPTS_DIR, "${ac_default_prefix}/${DATADIRNAME}/${PACKAGE}/scripts")
  scriptsdir="${ac_default_prefix}/${DATADIRNAME}/${PACKAGE}/scripts"
  AC_DEFINE_UNQUOTED(PIXMAPS_DIR, "${ac_default_prefix}/${DATADIRNAME}/${PACKAGE}/pixmaps")
  pixmapsdir="${ac_default_prefix}/${DATADIRNAME}/${PACKAGE}/pixmaps"
  AC_DEFINE_UNQUOTED(INTERFACES_DIR, "${ac_default_prefix}/${DATADIRNAME}/${PACKAGE}/interfaces")
  interfacesdir="${ac_default_prefix}/${DATADIRNAME}/${PACKAGE}/interfaces"
else
  AC_DEFINE_UNQUOTED(SCRIPTS_DIR, "${prefix}/${DATADIRNAME}/${PACKAGE}/scripts")
  scriptsdir="${prefix}/${DATADIRNAME}/${PACKAGE}/scripts"
  AC_DEFINE_UNQUOTED(PIXMAPS_DIR, "${prefix}/${DATADIRNAME}/${PACKAGE}/pixmaps")
  pixmapsdir="${prefix}/${DATADIRNAME}/${PACKAGE}/pixmaps"
  AC_DEFINE_UNQUOTED(INTERFACES_DIR, "${prefix}/${DATADIRNAME}/${PACKAGE}/interfaces")
  interfacesdir="${prefix}/${DATADIRNAME}/${PACKAGE}/interfaces"
fi

AC_SUBST(scriptsdir)
AC_SUBST(pixmapsdir)
AC_SUBST(interfacesdir)

AC_OUTPUT([
Makefile
ximian-setup-tools.spec
macros/Makefile
intl/Makefile
interfaces/Makefile
archiver/Makefile
archiver/location-manager-capplet/Makefile
backend/Makefile
pixmaps/Makefile
src/Makefile
src/common/Makefile
src/network/Makefile
src/shares/Makefile
src/disks/Makefile
src/time/Makefile
src/time/e-map/Makefile
src/memory/Makefile
src/users/Makefile
src/users/e-search-bar/Makefile
src/boot/Makefile
src/print/Makefile
src/internetsharing/Makefile
doc/Makefile
doc/nameresolution/Makefile
doc/networking/Makefile
doc/shares/Makefile
po/Makefile.in
])

if test "x${prefix}" != "x/usr" || test "x${sysconfdir}" != "x/etc"
then
  echo
  echo "*** The Ximian Setup Tools may not work with the default installation"
  echo "*** paths. If you're running Linux, and you installed most of the other"
  echo "*** GNOME components with a package manager, we suggest you configure"
  echo "*** this package like this:"
  echo "***"
  echo "*** ./configure --prefix=/usr --sysconfdir=/etc"
  echo "***"
  echo "*** This will be more likely to work. Thank you for your patience."
  echo
fi
