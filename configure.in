dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)

AM_INIT_AUTOMAKE(ximian-setup-tools, 0.7.0)
AM_CONFIG_HEADER(config.h)

dnl Pick up the Gnome macros.
AM_ACLOCAL_INCLUDE(macros)

GNOME_INIT
AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AM_PROG_LIBTOOL
AC_HEADER_STDC
AM_PROG_XML_I18N_TOOLS

GNOME_COMPILE_WARNINGS
GNOME_X_CHECKS
GNOME_XML_CHECK
AM_PATH_GDK_PIXBUF
AM_PATH_LIBGLADE(,,"gnome")

dnl =====================================================
dnl Cracklib checking
dnl =====================================================
AC_CHECK_LIB(crack, FascistCheck, ,[
  AC_MSG_WARN()
  AC_MSG_WARN(---------- Building without cracklib is not recommended. ----------)
  AC_MSG_WARN()
  AC_MSG_WARN(-- If you are looking for cracklib you can try  :)
  AC_MSG_WARN(--    http://www.users.dircon.co.uk/~crypto/)
  AC_MSG_WARN(--    or :)
  AC_MSG_WARN(--    http://www.rpmfind.net/linux/rpm2html/search.php?query=cracklib)
  AC_MSG_WARN(--    or :)
  AC_MSG_WARN(--    apt-get install cracklib2-dev)
  AC_MSG_WARN()])
if test x"$HAVE_LIBCRACK" = xyes ; then
  AC_CHECK_HEADER(crack.h, ,[
  AC_MSG_ERROR(cracklib crack.h header file not found.)])
fi

XST_CRACK_LIB_LOCATION="/usr/lib/"
AC_ARG_WITH(cracklib-dict,
[  --with-cracklib-dict[=DIR]       specify a cracklib dir ],[
  if test "$withval" != "no"; then
    XST_CRACK_LIB_LOCATION=$withval
  fi  
])

AC_CHECK_LIB(crypt, crypt, ,[
  AC_MSG_ERROR(crypt library is required.)])
AC_CHECK_HEADER(crypt.h,AC_DEFINE(HAVE_CRYPT_H))
AC_SUBST(XST_CRACK_LIB_LOCATION)
XST_CRACK_LIB_CFLAGS="-DXST_CRACK_LIB_DICT_PATH=\"\\\"\$(XST_CRACK_LIB_LOCATION)\\\"\""
AC_SUBST(XST_CRACK_LIB_CFLAGS)



dnl =====================================================
dnl END: Cracklib checking
dnl =====================================================

dnl =====================================================
dnl GdkPixbuf checking
dnl =====================================================

AC_MSG_CHECKING(for Gdkpixbuf libraries)
if gnome-config --libs gdk_pixbuf gnomecanvaspixbuf > /dev/null 2>&1; then 
    AC_MSG_RESULT(found)
else
    AC_MSG_ERROR(Did not find gdkpixbuf installed)
fi

dnl =====================================================
dnl END: GdkPixbuf checking
dnl =====================================================

dnl =====================================================
dnl Libglade check
dnl =====================================================

AC_MSG_CHECKING(for libglade libraries)
if gnome-config --libs libglade > /dev/null 2>&1; then
    AC_MSG_RESULT(found)
else
    AC_MSG_ERROR(Did not find libglade installed)
fi

dnl =====================================================
dnl END: Libglade check
dnl =====================================================

dnl =====================================================
dnl GAL checking
dnl =====================================================

AC_MSG_CHECKING(for Gnome App libraries (GAL) >= 0.5.99.12)
if gnome-config --libs gal > /dev/null 2>&1; then
	vers=`gnome-config --modversion gal | sed -e "s/gal-//" -e 's/cvs$//' -e 's/pre$//' | \
		 awk 'BEGIN { FS = "."; } { print $1 * 1000000 + $2 * 10000 + $3 * 100 + $4;}'`
	if test "$vers" -ge 59912; then
		AC_MSG_RESULT(found)
	 else
		AC_MSG_ERROR(You need at least GNOME Application libs 0.5.99.12 for this version of ximian-setup-tools)
	fi
else
	AC_MSG_ERROR(Did not find GnomeAppLib (GAL) installed)
fi
dnl =====================================================
dnl END: GAL checking
dnl =====================================================

dnl =====================================================
dnl Ximian archiver checking
dnl =====================================================

AC_MSG_CHECKING(for config_archiver >= 1.5.0)
if gnome-config --libs config_archiver > /dev/null 2>&1; then
	vers=`gnome-config --modversion config_archiver | sed -e "s/config-archiver-//" -e 's/cvs$//' -e 's/pre$//' | \
		 awk 'BEGIN { FS = "."; } { print $1 * 1000000 + $2 * 10000 + $3 * 100 + $4;}'`
	if test "$vers" -ge 1050000; then
		AC_MSG_RESULT(found)
	 else
		AC_MSG_ERROR(You need at least Control Center 1.5.0 for this version of ximian-setup-tools (CVS module gnome-control-center))
	fi
else
	   AC_MSG_ERROR(You need at least Control Center 1.5.0 for this version of ximian-setup-tools (CVS module gnome-control-center))
fi
dnl =====================================================
dnl END: Ximian Archiver
dnl =====================================================

dnl ========================================================
dnl Transtlations
dnl ========================================================

ALL_LINGUAS="az da de el es fr gl hu it ja ko nn no pl pt_BR ru sk sv tr uk wa zh_TW.Big5"
AM_GNOME_GETTEXT

dnl Set PACKAGE_LOCALE_DIR in config.h.
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale")
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale")
fi

dnl ========================================================
dnl END : Translations
dnl ========================================================

if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(SCRIPTS_DIR, "${ac_default_prefix}/${DATADIRNAME}/setup-tool-backends/scripts")
  scriptsdir="${ac_default_prefix}/${DATADIRNAME}/setup-tool-backends/scripts"
else
  AC_DEFINE_UNQUOTED(SCRIPTS_DIR,"${prefix}/${DATADIRNAME}/setup-tool-backends/scripts")
  scriptsdir="${prefix}/${DATADIRNAME}/setup-tool-backends/scripts"
fi

if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PIXMAPS_DIR, "${ac_default_prefix}/${DATADIRNAME}/${PACKAGE}/pixmaps")
  pixmapsdir="${ac_default_prefix}/${DATADIRNAME}/${PACKAGE}/pixmaps"
  AC_DEFINE_UNQUOTED(INTERFACES_DIR, "${ac_default_prefix}/${DATADIRNAME}/${PACKAGE}/interfaces")
  interfacesdir="${ac_default_prefix}/${DATADIRNAME}/${PACKAGE}/interfaces"
  AC_DEFINE_UNQUOTED(ETSPECS_DIR,"${ac_default_prefix}/${DATADIRNAME}/${PACKAGE}/etspecs")
  etspecsdir="${ac_default_prefix}/${DATADIRNAME}/${PACKAGE}/etspecs"
  AC_DEFINE_UNQUOTED(FRONTEND_DIR,"${ac_default_prefix}/${BINDIRNAME}")
  AC_DEFINE_UNQUOTED(DESKTOP_DATA_DIR,"${ac_default_prefix}/${DATADIRNAME}/control-center/capplets/System")
else
  AC_DEFINE_UNQUOTED(PIXMAPS_DIR, "${prefix}/${DATADIRNAME}/${PACKAGE}/pixmaps")
  pixmapsdir="${prefix}/${DATADIRNAME}/${PACKAGE}/pixmaps"
  AC_DEFINE_UNQUOTED(INTERFACES_DIR, "${prefix}/${DATADIRNAME}/${PACKAGE}/interfaces")
  interfacesdir="${prefix}/${DATADIRNAME}/${PACKAGE}/interfaces"
  AC_DEFINE_UNQUOTED(ETSPECS_DIR,"${prefix}/${DATADIRNAME}/${PACKAGE}/etspecs")
  etspecsdir="${prefix}/${DATADIRNAME}/${PACKAGE}/etspecs"
  AC_DEFINE_UNQUOTED(FRONTEND_DIR,"${prefix}/${BINDIRNAME}")
  AC_DEFINE_UNQUOTED(DESKTOP_DATA_DIR,"${prefix}/${DATADIRNAME}/control-center/capplets/System")
fi

dnl ==============================================================
dnl END: Set SCRIPTS_DIR & PIXMAPS_DIR 
dnl ==============================================================

dnl =====================================================
dnl XST_LIBS & XST_CFLAGS 
dnl =====================================================

XST_LIBS="`gnome-config --libs gal print gdk_pixbuf gnomecanvaspixbuf libglade xml config_archiver`"
XST_CFLAGS="`gnome-config --cflags gal print gdk_pixbuf gnomecanvaspixbuf libglade xml config_archiver`"

case $host in
*-*-freebsd* )
	XST_LIBS="$XST_LIBS -lutil"
	;;
esac;

XST_TOOL_LIBS="$XST_LIBS \$(top_builddir)/src/common/libsetuptool.a"
XST_TOOL_CFLAGS="$XST_CFLAGS -I\$(top_srcdir)/src/common"

XST_TOOL_CLEANFILES="\$(desktop) \$(desktop).in"
XST_TOOL_EXTRA_DIST="\$(toolpixmaps) \$(desktop).in.in \$(desktop).in \$(desktop) \$(etspecs)"

AC_DEFUN(AC_PROG_XST_TOOLS,
[
XST_TOOL_DESKTOP_IN_RULE='desktop = $(toolname).desktop\
$(desktop).in: \%.desktop.in: \%.desktop.in.in\
	sed s#Icon=.\*#Icon=$(pixmapsdir)/$(toolname).png# < $< \> $(toolname).desktop.in\
\
install-data-local:\
	$(mkinstalldirs) $(DESTDIR)$(datadir)/gnome/apps/Settings/System\
	$(INSTALL_DATA) $(desktop) $(DESTDIR)$(datadir)/gnome/apps/Settings/System/$(desktop)\
	$(mkinstalldirs) $(DESTDIR)$(datadir)/control-center/capplets/System\
	$(INSTALL_DATA) $(desktop) $(DESTDIR)$(datadir)/control-center/capplets/System/$(desktop)\
install-data-am: install-etspecsDATA install-data-local'
	
AC_DIVERT_PUSH(AC_DIVERSION_SED)dnl
s%@XST_TOOL_DESKTOP_IN_RULE@%[$]XST_TOOL_DESKTOP_IN_RULE%g
AC_DIVERT_POP()dnl
# Redirect the config.log output again, so that the ltconfig log is not
# clobbered by the next message.
exec 5>>./config.log
])
AC_PROG_XST_TOOLS

XST_TOOL_DEPENDENCIES="\$(top_builddir)/src/common/libsetuptool.a"

AC_SUBST(XST_LIBS)
AC_SUBST(XST_CFLAGS)
AC_SUBST(XST_TOOL_LIBS)
AC_SUBST(XST_TOOL_CFLAGS)
AC_SUBST(XST_TOOL_CLEANFILES)
AC_SUBST(XST_TOOL_EXTRA_DIST)
AC_SUBST(XST_TOOL_DEPENDENCIES)

dnl ==================================
dnl END: XST_LIBS & XST_FLAGS
dnl ==================================

AC_SUBST(scriptsdir)
AC_SUBST(pixmapsdir)
AC_SUBST(interfacesdir)
AC_SUBST(etspecsdir)


dnl ===========================
dnl Configure the backends
dnl ==========================

AC_CONFIG_SUBDIRS(backend)

AC_OUTPUT([
Makefile
ximian-setup-tools.spec
macros/Makefile
intl/Makefile
interfaces/Makefile
pixmaps/Makefile
src/Makefile
src/common/Makefile
src/disks/Makefile
src/network/Makefile
src/time/Makefile
src/time/e-map/Makefile
src/users/Makefile
src/users/e-search-bar/Makefile
src/boot/Makefile

src/shares/Makefile
src/print/Makefile
src/display/Makefile
src/location-manager/Makefile
src/internetsharing/Makefile

doc/Makefile
doc/nameresolution/Makefile
doc/networking/Makefile
doc/shares/Makefile
po/Makefile.in
])

echo "

Configuration (XST):

	The scripts are expected at : ${scriptsdir}
	Compiler:		${CC} 
"
